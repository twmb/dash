set terminal png size 1280,768 font "helvetica neue thin,12"

set boxwidth 0.30 absolute
set offset 0.5,0.5,0,0

set xrange [ 0 : 49 ]
set ylabel 'nanoseconds'
set xlabel 'cpu'
set logscale y

set key left

enqss = "100"
deqss = "100 10"

types  = "enq deq thr"
typesFull = "enqueue dequeue throughput"

do for [k=1:words(enqss)] {
    do for [j=1:words(deqss)] {
        do for [i=1:words(types)] {
            enqs = word(enqss, k)
            deqs = word(deqss, j)
            timings = word(types, i)
            set output sprintf('e%sd%s.%s.png', enqs, deqs, timings)
            set title sprintf('%s enq, %s deq, %s timings', enqs, deqs, word(typesFull, i))
        
            plot sprintf('e%sd%s.%s.channel', enqs, deqs, timings) using 1:1:xticlabels(1) lt -3 notitle,\
                   '' using ($1-0.30):3:2:6:5 with candlesticks lt 1 lw 3 title 'channel' whiskerbars,\
                   '' using ($1-0.30):4:4:4:4 with candlesticks lt -1 lw 3 notitle,\
                 sprintf('e%sd%s.%s.mpmcdvq', enqs, deqs, timings) using ($1+0.30):3:2:6:5 with candlesticks lt 3 lw 3 title 'mpmcdvq' whiskerbars,\
                   '' using ($1+0.30):4:4:4:4 with candlesticks lt -1 lw 3 notitle
        }
    }
}
